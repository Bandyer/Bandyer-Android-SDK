/*
 * Copyright (C) 2020 Bandyer S.r.l. All Rights Reserved.
 * See LICENSE.txt for licensing information
 */

buildscript {
    repositories {
        google()
        maven { url 'https://jitpack.io' }
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://developer.huawei.com/repo/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
    dependencies {
        classpath 'com.github.triplet.play:com.github.triplet.play.gradle.plugin:3.7.0'
        classpath 'com.google.firebase:firebase-appdistribution-gradle:3.0.3'
        classpath 'com.huawei.agconnect:agcp:1.6.5.300'
        classpath "ru.cian:huawei-publish-gradle-plugin:1.3.3"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: 'versions.gradle'
apply plugin: "com.github.triplet.play"
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'ru.cian.huawei-publish'
apply plugin: 'com.google.firebase.crashlytics'

def leakCanaryVersion = '2.7'
def jUnitLibVersion = '4.13.2'
def multiDexLibVersion = "2.0.1"
def firebaseMessagingLibVersion = '23.0.7'
def firebaseMessagingIidLibVersion = '21.1.0'
def crashlyticsLibVersion = "18.2.12"
def work_version = '2.7.1'
def bandyerLibVersion = "3.2.3"
def bandyerExtensionUsbCamera = "10.0.5"
def pushy_version = '1.0.82'
def huawei_push_version = '6.5.0.300'
def androidX = '1.1.0'
def desugaring_version = "1.1.5"

android {
    compileSdkVersion sdkVersion
    buildToolsVersion buildToolsVersion

    defaultConfig {
        applicationId "com.bandyer.demo_android_sdk"
        minSdkVersion 21
        targetSdkVersion sdkVersion
        versionCode VERSION_CODE.toInteger()
        versionName bandyerLibVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            if (keyStoreFilePath(name)?.trim()) {
                keyAlias keyStoreAlias(name)
                keyPassword keyStorePassword(name)
                storeFile file(keyStoreFilePath(name))
                storePassword keyStorePassword(name)
            }
        }
        release {
            if (keyStoreFilePath(name)?.trim()) {
                keyAlias keyStoreAlias(name)
                keyPassword keyStorePassword(name)
                storeFile file(keyStoreFilePath(name))
                storePassword keyStorePassword(name)
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keyStoreFilePath(name)?.trim()) signingConfig signingConfigs.release
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
            if (keyStoreFilePath(name)?.trim()) signingConfig signingConfigs.debug

            // Firebase distribution
            if (getDistributionCredentialsFilePath(name)?.trim()) firebaseAppDistribution {
                appId getDistributionAppId(name)
                serviceCredentialsFile getDistributionCredentialsFilePath(name)
                releaseNotes getDistributionReleaseNotes(bandyerLibVersion)
                groups getDistributionTestGroup()
            }
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    ndkVersion "23.1.7779620"
}

huaweiPublish {
    instances {
        release {
            credentialsPath = getHmsDistributionCredentialsFilePath()
            buildFormat = "aab"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugaring_version"
    /*** Platform ***/
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.multidex:multidex:$multiDexLibVersion"
    implementation "androidx.appcompat:appcompat:$androidX"

    /*** Testing ***/
    testImplementation "junit:junit:$jUnitLibVersion"

    /*** Utilities & Debug Tools ***/
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"

    /*** Notification ***/
    implementation "com.google.firebase:firebase-messaging:$firebaseMessagingLibVersion"
    implementation("com.google.firebase:firebase-iid:$firebaseMessagingIidLibVersion")
    implementation "com.google.firebase:firebase-crashlytics-ndk:$crashlyticsLibVersion"
    implementation "androidx.work:work-runtime:$work_version"

    /*** Notification alternative ***/
    implementation "me.pushy:sdk:$pushy_version"
    implementation "com.huawei.hms:push:$huawei_push_version"

    /** Utilities **/
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation project(":app-utilities")
    implementation project(":app-configuration")
    implementation project(":app-design")

    /*** Bandyer Android SDK ***/
    implementation "com.bandyer:bandyer-android-sdk:$bandyerLibVersion"

    // define this dependency only if you desire to handle usb camera input in a video call
    implementation "com.bandyer:extension_usb_camera:$bandyerExtensionUsbCamera"
}

play {
    def canPublish = !playStoreCredentialsFilePath()?.trim().allWhitespace
    if (canPublish) serviceAccountCredentials = file(playStoreCredentialsFilePath())
    track.set("beta")
    enabled.set(canPublish)
    defaultToAppBundles.set(true)
}